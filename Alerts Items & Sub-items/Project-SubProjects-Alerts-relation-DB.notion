/* v5.2 - (DB)
oem at oemden dot com
- Alert Messages as Variables
----------------------------------------
- Needs a relation to Alert_level-DB ->

-> Alert_level-DB:
			|Alert_level(Name)|Setting(Select)|Description(text - optionnal)|
			|Projects|All Alerts|Displays All Messages [KO + OK]

Settings is a 'Select' Property with 3 'Options':
â€¢ All Alerts
â€¢ Alerts
â€¢ No Alerts

To display:
		â€¢ ONLY [KO] Status, set Option to empty "Alerts"
		â€¢ ALL Status [KO & OK], set ALL_Cases to "All Alerts"
		â€¢ No Status [KO & OK], set ALL_Cases to "No Alerts"
*/

/* ---- lets start -----	*/
lets (

	Projet_sans_sub_sans_Task, /* Case1 */
	prop("Sub-item").empty() and prop("Tasks").empty() and prop("Parent item").empty(),

	Projet_sans_sub_avec_Task,  /* Case 6 */
	prop("Sub-item").empty() and prop("Tasks").empty().not() and prop("Parent item").empty(),

	Projet_avec_sub, /* intermediary */
	prop("Sub-item").empty().not(),

	Sub_sans_task, /*intermediary */
	prop("Sub-item").filter(current.prop("Tasks").empty()),

	Projet_avec_sub_sans_task,
	Projet_avec_sub == true and Sub_sans_task == true,

	ProjectSub_sans_task, /* Case 2 */
	prop("Parent item").empty().not() and prop("Tasks").empty(),

	Sub_avec_task, /* Case 3 */
	prop("Parent item").empty().not() and prop("Tasks").empty().not(),

	Alert_Settings,
	prop("Alerts Level").map(current.prop("Setting")).format(),

	Alert_Settings_none,
	Alert_Settings == "No Alerts",

	Alert_Settings_all,
	Alert_Settings  == "All Alerts",

	Alert_Settings_std,
	Alert_Settings == "Alerts",

	Case1Alert,
	" ðŸš¨ This Project has No Tasks assigned ! ",

	Case2Alert,
	" ðŸš¨ This Sub-Project has No Tasks assigned ! ",

	Case3Alert,
	" âœ… This Sub-Project has Tasks Assigned ! ",

	Case4Alert,
	" ðŸš¨Â One or more Sub-Project has no task assigned ! ",

	Case5Alert,
	" âœ…Â All Sub-Projects have tasks assigned! ",

	Case6Alert,
	" âœ…Â This Project has Tasks assigned!",

	/* ---- Conditions start -----	*/

	ifs(
		Alert_Settings_none,
		"",

		Alert_Settings_all,
			ifs(
				/* Case1: Standalone Project With No Sub-Project and No Task: Alert*/
				Projet_sans_sub_sans_Task == true, Case1Alert.style("b", "red", "red_background"),

				/* Case 2: Sub-Project with No Task: Alert */
				ProjectSub_sans_task == true, Case2Alert.style("b", "pink", "pink_background"),

				/* Case 4: main Project has one or more Sub-Project with No Task assigned */
				Projet_avec_sub == true and prop("Sub-item").filter(current.prop("Tasks").empty()), Case4Alert.style("b", "red", "red_background"),

				/* Case 3: Sub Project has Tasks Assigned */
        Sub_avec_task == true, Case3Alert.style("b", "blue", "blue_background"),

      	/* Case 5: Main Project has all Sub-Projects with Task assigned */
    		Projet_avec_sub == true and prop("Sub-item").filter(current.prop("Tasks").empty().not()), Case5Alert.style("b", "green", "green_background"),

    		/* Case 6: Standalone Project with no sub-project AND Tasks Assigned */
    		Projet_sans_sub_avec_Task == true, Case6Alert.style("b", "green", "green_background")
			),

		Alert_Settings_std,
			ifs(
					/* Case1: Standalone Project With No Sub-Project and No Task: Alert*/
					Projet_sans_sub_sans_Task == true, Case1Alert.style("b", "red", "red_background"),

					/* Case 2: Sub-Project with No Task: Alert */
					ProjectSub_sans_task == true, Case2Alert.style("b", "pink", "pink_background"),

					/* Case 4: main Project has one or more Sub-Project with No Task assigned */
					Projet_avec_sub == true and prop("Sub-item").filter(current.prop("Tasks").empty()), Case4Alert.style("b", "red", "red_background")
			)
  )
	/* ---- Conditions end -----	*/
)
/* ---- lets end -----	*/
